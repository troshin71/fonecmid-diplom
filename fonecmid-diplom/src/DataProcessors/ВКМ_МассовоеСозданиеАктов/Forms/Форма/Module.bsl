#Область ОбработчикиКоманд

&НаКлиенте
Процедура ВКМ_Заполнить(Команда)
	
	// Проверка заполнения периода
	Если Не ЗначениеЗаполнено(Объект.ВКМ_Период) Тогда
		ПоказатьПредупреждение(, "Выберите период (месяц)!");
		Возврат;
	КонецЕсли;
	
	ВКМ_ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВКМ_Создать(Команда)
	
	Если Объект.ВКМ_СписокДоговоров.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Список договоров пуст.");
		Возврат;
	КонецЕсли;
	
	ВКМ_СоздатьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВКМ_ЗаполнитьНаСервере()
	
	Объект.ВКМ_СписокДоговоров.Очистить();
	
	// Ищем все действующие договоры "Абонентское обслуживание"
	// и не помеченные на удаление.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Договоры.Ссылка КАК Договор
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК Договоры
		|ГДЕ
		|	Договоры.ВидДоговора = &ВидДоговора
		|	И НЕ Договоры.ПометкаУдаления";
		
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.ВКМ_СписокДоговоров.Добавить();
		НоваяСтрока.ВКМ_Договор = Выборка.Договор;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВКМ_СоздатьНаСервере()
	
	Для Каждого СтрокаТЧ Из Объект.ВКМ_СписокДоговоров Цикл
		
		// Если документ уже создан в этой строке - пропускаем
		Если ЗначениеЗаполнено(СтрокаТЧ.ВКМ_Реализация) Тогда
			Продолжить;
		КонецЕсли;
		
		// Создаем новый документ
		НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		
		// Заполняем шапку
		// Датой документа ставим конец выбранного месяца
		НовыйДок.Дата = КонецМесяца(Объект.ВКМ_Период); 
		НовыйДок.Контрагент = СтрокаТЧ.ВКМ_Договор.Владелец; 
		НовыйДок.Договор = СтрокаТЧ.ВКМ_Договор;
		НовыйДок.Организация = СтрокаТЧ.ВКМ_Договор.Организация;
		НовыйДок.Ответственный = Пользователи.ТекущийПользователь();
		
		НовыйДок.ВКМ_ВыполнитьАвтозаполнение();
		
		
		Если НовыйДок.Услуги.Количество() > 0 Тогда
			НовыйДок.Записать(РежимЗаписиДокумента.Проведение);
			// Сохраняем ссылку в таблицу обработки
			СтрокаТЧ.ВКМ_Реализация = НовыйДок.Ссылка;
		Иначе
			// Если документ пустой (нет абон. платы и работ) - не сохраняем
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти