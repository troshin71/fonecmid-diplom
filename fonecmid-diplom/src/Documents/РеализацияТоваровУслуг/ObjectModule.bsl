
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область СлужебныеПроцедурыИФункции

// ВКМ_Начало
// Процедура заполняет табличную часть "Услуги" по данным договора и регистра накопления
Процедура ВКМ_ВыполнитьАвтозаполнение() Экспорт
	
	// 1. Очищаем табличную часть перед заполнением
	Услуги.Очистить();
	
	// 2. Получаем номенклатуру из констант
	НоменклатураАбПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	НоменклатураРаботы  = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	
	// Если константы не заполнены — выбрасываем исключение (оно покажется пользователю как ошибка)
	Если НЕ ЗначениеЗаполнено(НоменклатураАбПлата) ИЛИ НЕ ЗначениеЗаполнено(НоменклатураРаботы) Тогда
		ВызватьИсключение "Не заполнены константы номенклатуры для автозаполнения (ВКМ)! Проверьте настройки программы.";
	КонецЕсли;
	
	// 3. Проверяем вид договора. Если это не "Абонентское обслуживание", ничего не делаем.
	Если Договор.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		Возврат;
	КонецЕсли;
	
	// Берем сумму прямо из карточки договора
	Если Договор.ВКМ_СуммаАбонентскойПлаты > 0 Тогда
		НоваяСтрока = Услуги.Добавить();
		НоваяСтрока.Номенклатура = НоменклатураАбПлата;
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Цена = Договор.ВКМ_СуммаАбонентскойПлаты;
		НоваяСтрока.Сумма = Договор.ВКМ_СуммаАбонентскойПлаты;
	КонецЕсли;
	
	// Собираем данные из регистра накопления за месяц даты документа
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_КоличествоЧасовОборот) КАК Часы,
		|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_СуммаКОплатеОборот) КАК Сумма
		|ИЗ
		|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			ВКМ_Клиент = &Клиент
		|				И ВКМ_Договор = &Договор) КАК ВКМ_ВыполненныеКлиентуРаботыОбороты";
	
	// Устанавливаем параметры: Начало и Конец месяца даты документа
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Клиент", Контрагент);
	Запрос.УстановитьПараметр("Договор", Договор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Если данные в регистре есть
	Если Выборка.Следующий() Тогда
		// Проверяем, что есть часы или сумма 
		Если Выборка.Сумма > 0 ИЛИ Выборка.Часы > 0 Тогда
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Номенклатура = НоменклатураРаботы;
			НоваяСтрока.Количество = Выборка.Часы;
			НоваяСтрока.Сумма = Выборка.Сумма;
			
			// Вычисляем цену 
			Если НоваяСтрока.Количество <> 0 Тогда
				НоваяСтрока.Цена = НоваяСтрока.Сумма / НоваяСтрока.Количество;
			КонецЕсли;
			
			НоваяСтрока.Содержание = "Работы специалиста за " + Формат(Дата, "ДФ='ММММ гггг'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
// ВКМ_Конец

#КонецОбласти
